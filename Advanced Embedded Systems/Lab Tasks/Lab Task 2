#include <OneWire.h>
#include <DallasTemperature.h>
#include <SPI.h>
#include <WiFiNINA.h>
#include "arduino_secrets.h"

const char AP_SSID[] = SECRET_SSID;
const char AP_PASS[] = SECRET_PASS;

const uint8_t LED_PIN = LED_BUILTIN;
int wifiState = WL_IDLE_STATUS;

WiFiServer web(80);

constexpr uint8_t TEMP_PIN = 4;
OneWire dsWire(TEMP_PIN);
DallasTemperature tempSensor(&dsWire);

void showStatus() {
  Serial.print("SSID: ");          Serial.println(WiFi.SSID());
  IPAddress ip = WiFi.localIP();   Serial.print("IP Address: "); Serial.println(ip);
  Serial.print("Open http://");    Serial.println(ip);
}

void setup() {
  Serial.begin(9600); while (!Serial) {}
  tempSensor.begin();
  pinMode(LED_PIN, OUTPUT);

  if (WiFi.status() == WL_NO_MODULE) while (true);

  wifiState = WiFi.beginAP(AP_SSID, AP_PASS);
  if (wifiState != WL_AP_LISTENING) while (true);

  delay(10000);
  web.begin();
  showStatus();
}

void loop() {
  tempSensor.requestTemperatures();
  Serial.print("Temperature: "); Serial.print(tempSensor.getTempCByIndex(0)); Serial.println(" Â°C");
  delay(1000);

  int cur = WiFi.status();
  if (cur != wifiState) {
    wifiState = cur;
    Serial.println(wifiState == WL_AP_CONNECTED ? "Device connected to AP" : "Device disconnected from AP");
  }

  WiFiClient client = web.available();
  if (client) {
    String line;
    while (client.connected()) {
      if (client.available()) {
        char c = client.read();
        if (c == '\n') {
          if (line.length() == 0) {
            client.println("HTTP/1.1 200 OK");
            client.println("Content-type:text/html");
            client.println();
            client.print("Click <a href=\"/ON\">here</a> to switch LED on<br>");
            client.print("Click <a href=\"/OFF\">here</a> to switch LED off<br>");
            client.println();
            break;
          } else line = "";
        } else if (c != '\r') line += c;

        if (line.endsWith("GET /ON"))  digitalWrite(LED_PIN, HIGH);
        if (line.endsWith("GET /OFF")) digitalWrite(LED_PIN, LOW);
      }
    }
    client.stop();
  }
}
